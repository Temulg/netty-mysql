plugins {
	id 'java'
	id 'eclipse'
}

group 'udentric'
version '1.0'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url 'https://repo.udentric.net/maven'
	}
}

dependencies {
	compile 'org.apache.logging.log4j:log4j-api:2.10.0'
	compile 'org.apache.logging.log4j:log4j-core:2.10.0'
	compile 'com.google.guava:guava:24.0-jre'
	compile 'io.netty:netty-handler:4.1.22.Final'

	testCompile 'org.testng:testng:6.13.1'
	testCompile 'udentric:assert:1.0'
}

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
	}
}

sourceSets {
	testsuite {
		java {
			srcDirs = ['testsuite/test/java']
		}
		resources {
			srcDirs = ['testsuite/test/resources']
		}
		compileClasspath += sourceSets.test.compileClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
	}
}

compileJava {
	options.encoding = 'UTF-8'
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

task docJar(type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
}

tasks.withType(Test) {task ->
	project.properties.each{k, v ->
		if (k.startsWith("udentric.")) {
			task.systemProperty(k, v)
		}
	}
}

task testsuiteRun(type: Test, dependsOn: 'cleanTestsuiteRun') {
	useTestNG()
	testLogging.showStandardStreams = true
	setTestClassesDirs sourceSets.testsuite.output
	classpath = sourceSets.testsuite.runtimeClasspath
}

task run(type: JavaExec) {
	main = project.properties.getOrDefault('mainClass', '')
	args project.properties.getOrDefault('cmdLineArgs', '')
	jvmArgs project.properties.getOrDefault('jvmLineArgs', '')
	classpath = sourceSets.test.runtimeClasspath
}

task debug(type: JavaExec) {
	main = project.properties.getOrDefault('mainClass', '')
	args project.properties.getOrDefault('cmdLineArgs', '')
	jvmArgs project.properties.getOrDefault('jvmLineArgs', '')
	classpath = sourceSets.test.runtimeClasspath
	standardInput = System.in
	ignoreExitValue = true
	debug = true
}
